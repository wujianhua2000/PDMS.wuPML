
--	...........................................................................
--	第一版在上海完成，初步掌握实体的数据格式。
--
--	2019-06-18 15:59, tian-an 601, wujianhua
--	rev.2
--
--
--
--	...........................................................................

!this		= !!CE
!currENT	= !!CE

!X = 1

do !X
	--	直到 SITE 层
	break	if ( !this.type  EQ  'SITE' )
--	break	if ( !this.type  EQ  'ZONE' )

	!!CE	= !!CE.owner
	!this	= !!CE

enddo

--	...........................................................................

VAR !listgfloor COLL ALL	floor	FOR CE  

--	Q VAR !wallist

!pathname = |D:\123-PDMS|

!swriter = object FILE(  !pathname + '\pdms-listing-gene-floor.xml' )
!swriter.open( 'WRITE' )

!swriter.writerecord( '<?xml version="1.0" encoding="utf-8" ?>' )
!swriter.writerecord( '<ListGeneFloor>' )

do  !nname  values  !listgfloor

	--	这里 !nname 是字符串，所以需要转换，使用	DBREF() 
	!entity	= !nname.DBREF()

	--	这个 POSITION 相当于是平行移动，然后以 2D 的方式绘制平面图进行拉伸。
	--
	!post	= !entity.position

	--	如何定位到上级有 position， orientation
	--	当前 entity 的 orientation 中记录了上级（父节点）的原点来源
	--	记录父节点坐标系的目的是为了以后制图的时候，方便在一个合适的坐标系内进行计算。
	--	比如：角度，间距等等。
	--
	!vatiET = !entity.orientation.origin

	!zeroPT = !vatiET.position.wrt( world )
	!orient = !vatiET.orientation.wrt( world )
	
	!swriter.writerecord( '<GeneFloor>'				)
	!swriter.writerecord( !!wuXmlString(		|Name|, 		!entity.name 			) )
	!swriter.writerecord( !!wuXmlString(		|RefNo|, 		!entity.RefNo.String()	) )
	!swriter.writerecord( !!wuXmlString(		|Type|, 		!entity.Type		 	) )
	!swriter.writerecord( !!wuXmlString( 		|Vati-Name|,	!entity.owner.name 		) )
	!swriter.writerecord( !!wuXmlString( 		|Vati-Type|,	!entity.owner.type 		) )

	!swriter.writerecord( !!wuXmlPoint(			|Original|,		!zeroPT					) )
	!swriter.writerecord( !!wuXmlOrientation(	|Orientation|,	!orient					) )
	!swriter.writerecord( !!wuXmlPoint(			|Position|,		!post					) )

	!listsohn = !entity.members

	do !plname values !listsohn

		--	已经是 object 了，无需 DBREF()
		!ptr	= !plname
		
		if !ptr.type EQ 'PLOO' then

			--	sjust	surface justification for PAL Js	平面的对齐方式
			--
			!depth		= !ptr.height
			!sjust		= !ptr.sjust
			!listpnts	= !ptr.members

			!swriter.writerecord( !!wuXmlString( |FaceJust|, 	!sjust 					) )
			!swriter.writerecord( !!wuXmlDouble( |Height|, 		!depth 					) )
			!swriter.writerecord( '<Points>'  )
			
			!idx = 1
			do !ptname values !listpnts
				!ptr	= !ptname
				!post	= !ptr.position
			
				!swriter.writerecord( !!wuXmlIndexPoint( |point|, !idx, !post ) )
				!idx	= !idx + 1
			enddo
			
			!swriter.writerecord( '</Points>'  )

		endif
	
	enddo

	!swriter.writerecord( '</GeneFloor>'		)

enddo

!swriter.writerecord( '</ListGeneFloor>' )
!swriter.close( )

--	............................................................................

!!CE	= !currENT
