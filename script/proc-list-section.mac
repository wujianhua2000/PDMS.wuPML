
--	...........................................................................
--	第一版在上海完成，初步掌握实体的数据格式。
--
--	2019-06-19 11:09, tian-an 601, wujianhua
--	rev.2
--
--
--
--	............................................................................

!this		= !!CE
!currENT	= !!CE

!X = 1

do !X
	--	直到 ZONE 层
	break	if ( !this.type  EQ  'ZONE' )

	!!CE	= !!CE.owner
	!this	= !!CE

enddo

--	............................................................................

VAR	  !listentities	COLL  ALL	SCTN	FOR CE  

--	Q VAR !wallist

!pathname = |D:\123-PDMS|

!swriter = object FILE(  !pathname + '\pdms-listing-section.xml' )
!swriter.open( 'WRITE' )

--	............................................................................

!swriter.writerecord( '<?xml version="1.0" encoding="utf-8" ?>' )
!swriter.writerecord( '<ListSection>' )

do  !nname  values  !listentities

	!entity	= !nname.DBREF()
	
	--	这个 POSITION 相当于是平行移动，然后以 2D 的方式绘制平面图进行拉伸。
	--
	!post	= !entity.position

	--	如何定位到上级有 position， orientation
	--	当前 entity 的 orientation 中记录了上级（父节点）的原点来源
	--	记录父节点坐标系的目的是为了以后制图的时候，方便在一个合适的坐标系内进行计算。
	--	比如：角度，间距等等。
	--
	!vatiET = !entity.orientation.origin

	!zeroPT = !vatiET.position.wrt( world )
	!orient = !vatiET.orientation.wrt( world )

	!speREF	= !entity.spref

	!swriter.writerecord( '<Section>'	)
	
	!swriter.writerecord( !!wuXmlString(		|Name|, 				!entity.name 			) )
	!swriter.writerecord( !!wuXmlString(		|RefNo|, 				!entity.RefNo.String()	) )
	!swriter.writerecord( !!wuXmlString(		|Type|, 				!entity.Type		 	) )
	!swriter.writerecord( !!wuXmlString(		|Gene-Type|,			!entity.GType		 	) )
	!swriter.writerecord( !!wuXmlString( 		|Vati-Name|,			!entity.owner.name 		) )
	!swriter.writerecord( !!wuXmlString( 		|Vati-Type|,			!entity.owner.type 		) )

	!swriter.writerecord( !!wuXmlPoint(			|Original|,					!zeroPT				) )
	!swriter.writerecord( !!wuXmlOrientation(	|Orientation|,				!orient				) )
	
	!swriter.writerecord( !!wuXmlPoint(			|Point1|,					!entity.posstart	) )
	!swriter.writerecord( !!wuXmlPoint(			|Point2|,					!entity.posend		) )
	
	!swriter.writerecord( !!wuXmlDirection( 	|Vector1|,					!entity.drnstart	) )
	!swriter.writerecord( !!wuXmlDirection( 	|Vector2|,					!entity.drnend		) )

	!swriter.writerecord( !!wuXmlDouble( 		|BetaAngle|,				!entity.bangle		) )

	!swriter.writerecord( !!wuXmlString(		|Joining-Line|,				!entity.jline		) )
	!swriter.writerecord( !!wuXmlString(		|Justification-Pline|,		!entity.jusline		) )
	!swriter.writerecord( !!wuXmlString(		|Section-Line|,				!entity.memline		) )

	--.....................................................

	!swriter.writerecord( '<Ref-Spec>' )
	!swriter.writerecord( !!wuXmlString(		|Spec-Name|, 				!speREF.name		) )
	
	!swriter.writerecord( '<Ref-Cate>' )

	!!wuXmlRefGmss( !entity, !swriter )
	!!wuXmlRefPtss( !entity, !swriter )
	!!wuXmlRefData( !entity, !swriter )

	!swriter.writerecord( '</Ref-Cate>' )
	!swriter.writerecord( '</Ref-Spec>' )
	
	--.....................................................
	
	!swriter.writerecord( '</Section>' )

enddo

--	............................................................................

!swriter.writerecord( '</ListSection>' )
!swriter.close( )

--	............................................................................

!!CE	= !currENT

