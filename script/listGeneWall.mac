
--	............................................................................

!this	= !!CE

!X = 1

do !X
	--	直到 SITE 层
	break	if ( !this.type  EQ  'SITE' )
--	break	if ( !this.type  EQ  'ZONE' )

	!!CE	= !!CE.owner
	!this	= !!CE

enddo

--	............................................................................

VAR !listgwall COLL ALL	GWALL	FOR CE  

--	Q VAR !wallist

!swriter = object FILE( 'e:\W3D\list-gene-wall.xml' )
!swriter.open( 'WRITE' )

!swriter.writerecord( '<?xml version="1.0" encoding="utf-8" ?>' )
!swriter.writerecord( '<listgenewall>' )

do  !nname  values  !listgwall

	--	这里 !nname 是字符串，所以需要转换，使用	DBREF() 
	!gwall	= !nname.DBREF()
	!post	= !gwall.position.wrt( world )
	!vect	= !gwall.orientation.wrt( world )

	!swriter.writerecord( '<genewall>'		)
	!swriter.writerecord( !!wuXmlString( |name|,		!gwall.name ) )
	!swriter.writerecord( !!wuXmlString( |vati-name|,	!gwall.owner.name ) )
	!swriter.writerecord( !!wuXmlString( |vati-type|,	!gwall.owner.type ) )
	!swriter.writerecord( !!wuXmlPoint(	 |position|,	!post		) )
	!swriter.writerecord( !!wuXmlVector( |orientation|,	!vect		) )

	!listsohn = !gwall.members

	do !plname values !listsohn

		--	已经是 object 了，无需 DBREF()
		!ptr	= !plname
		
		if !ptr.type EQ 'PLOO' then

			!depth		= !ptr.height
			!listpnts	= !ptr.members

			!swriter.writerecord( !!wuXmlDouble( |height|, !depth ) )
			!swriter.writerecord( '<listpoint>'  )
			!idx = 1
			do !ptname values !listpnts
				!ptr	= !ptname
				!post	= !ptr.position
			
				!swriter.writerecord( !!wuXmlIndexPoint( |point|, !idx, !post ) )
				!idx	= !idx + 1
			enddo
			!swriter.writerecord( '</listpoint>'  )

		endif
	
	enddo

	!swriter.writerecord( '</genewall>'		)

enddo

!swriter.writerecord( '</listgenewall>' )
!swriter.close( )

--	............................................................................

