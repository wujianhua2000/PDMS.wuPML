
--	...........................................................................
--	第一版在上海完成，初步掌握实体的数据格式。
--
--	2019-06-19 15:23, tian-an 601, wujianhua
--	rev.2
--
--
--
--	............................................................................

!this		= !!CE
!currENT	= !!CE

!X = 1

do !X
	--	直到 ZONE 层
	break	if ( !this.type  EQ  'ZONE' )

	!!CE	= !!CE.owner
	!this	= !!CE

enddo

--	............................................................................
--
var !nboxlist 	coll all  nbox 	for ce

!pathname = |D:\123-PDMS|

!swriter = object FILE(  !pathname + '\pdms-listing-box-negtive.xml' )
!swriter.open( 'WRITE' )

!swriter.writerecord( '<?xml version="1.0" encoding="utf-8" ?>' )
!swriter.writerecord( '<ListBoxNegtive>' )

do  !nname  values  !nboxlist

	!entity = !nname.DBREF()

	--	这个 POSITION 相当于是平行移动，然后以 2D 的方式绘制平面图进行拉伸。
	--
	--	如何定位到上级有 position， orientation
	--	当前 entity 的 orientation 中记录了上级（父节点）的原点来源
	--	记录父节点坐标系的目的是为了以后制图的时候，方便在一个合适的坐标系内进行计算。
	--	比如：角度，间距等等。
	--
	!vatiET = !entity.orientation.origin

	!zeroPT = !vatiET.position.wrt( world )
	!orient = !vatiET.orientation.wrt( world )

	!swriter.writerecord( '<Box>'		)

	!swriter.writerecord( !!wuXmlString(		|Name|, 				!entity.name 			) )
	!swriter.writerecord( !!wuXmlString(		|RefNo|, 				!entity.RefNo.String()	) )
	!swriter.writerecord( !!wuXmlString(		|Type|, 				!entity.Type		 	) )
	!swriter.writerecord( !!wuXmlString( 		|Vati-Name|,			!entity.owner.name 		) )
	!swriter.writerecord( !!wuXmlString( 		|Vati-Type|,			!entity.owner.type 		) )
	
	!swriter.writerecord( !!wuXmlString(		|Negative|, 			|true|					) )

	!swriter.writerecord( !!wuXmlPoint(			|Original|,				!zeroPT					) )
	!swriter.writerecord( !!wuXmlOrientation(	|Orientation|,			!orient					) )

	!swriter.writerecord( !!wuXmlPoint(			|Position|,				!entity.position		) )

	!swriter.writerecord( !!wuXmlDouble(		|LenX|,					!entity.xlength			) )
	!swriter.writerecord( !!wuXmlDouble(		|LenY|,					!entity.ylength			) )
	!swriter.writerecord( !!wuXmlDouble(		|LenZ|,					!entity.zlength			) )
	
	!swriter.writerecord( '</Box>'		)
	
enddo

--	............................................................................

!swriter.writerecord( '</ListBoxNegtive>' )
!swriter.close( )

--	............................................................................

!!CE	= !currENT

