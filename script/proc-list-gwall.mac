
--	...........................................................................
--	第一版在上海完成
--
--	2019-06-10 13:39, tian-an 601, wujianhua
--	rev.2
--
--	GWALL 是一种由多个点围起来的面拉伸的墙体（实体）
--
--	...........................................................................

!this	= !!CE

!X = 1

do !X
	--	直到 SITE 层
	break	if ( !this.type  EQ  'SITE' )
--	break	if ( !this.type  EQ  'ZONE' )

	!!CE	= !!CE.owner
	!this	= !!CE

enddo

--	............................................................................

VAR !listgwall COLL ALL	gwall	FOR CE  

--	Q VAR !wallist

!pathname = |D:\123-PDMS|

!swriter = object FILE(  !pathname + '\pdms-Gwall-list.xml' )
!swriter.open( 'WRITE' )

!swriter.writerecord( '<?xml version="1.0" encoding="utf-8" ?>' )
!swriter.writerecord( '<listgwall>' )

do  !nname  values  !listgwall

	!gwall	= !nname.DBREF()
	!post	= !gwall.position.wrt( world )
	!vect	= !gwall.orientation.wrt( world )

	!swriter.writerecord( '<gwall>'		)
	
	!swriter.writerecord( !!wuXmlString(	|name|, 		!gwall.name 		) )
	!swriter.writerecord( !!wuXmlString(	|vati-name|, 	!gwall.owner.name	) )
	!swriter.writerecord( !!wuXmlString(	|vati-type|, 	!gwall.owner.type	) )
	!swriter.writerecord( !!wuXmlPoint(	 	|position|,		!post				) )
	!swriter.writerecord( !!wuXmlVector( 	|orientation|,	!vect				) )
	
	!listsohn = !gwall.members

	do !plname values !listsohn

		!ptr	= !plname
		
		if !ptr.type EQ 'PLOO' then

			!depth		= !ptr.height
			!listpnts	= !ptr.members

			!swriter.writerecord( !!wuXmlDouble( |height|, !depth ) )

			--	...........................................
			--	局部坐标系下的边界点集合
			--
			!swriter.writerecord( '<local-points>'  )
			
			!idx = 1
			do !ptname values !listpnts
				!ptr	= !ptname
				!post	= !ptr.position
				!swriter.writerecord( !!wuXmlIndexPoint( |point|, !idx, !post ) )
				!idx	= !idx + 1
			enddo
			
			!swriter.writerecord( '</local-points>'  )

			--	...........................................
			--
			--	GLOBAL 坐标系下的边界点集合
			--
			!swriter.writerecord( '<points>'  )
			
			!idx = 1
			do !ptname values !listpnts
				!ptr	= !ptname
				!post	= !ptr.position.wrt( world )
				!swriter.writerecord( !!wuXmlIndexPoint( |point|, !idx, !post ) )
				!idx	= !idx + 1
			enddo
			
			!swriter.writerecord( '</points>'  )
			
			--	...........................................
			
		endif
	
	enddo

	!swriter.writerecord( '</gwall>' )

enddo

!swriter.writerecord( '</listgwall>' )
!swriter.close( )

--	............................................................................
